apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// Load keystore
def keystorePropertiesFile = rootProject.file("../../../vendor/keys/keystore.properties")

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.katsuna.calls"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 27
        versionName "2.3.17"
        project.ext.set("archivesBaseName", "Calls-" + defaultConfig.versionName)
        manifestPlaceholders = [providers_auth:"com.katsuna"]
    }

    if (keystorePropertiesFile.exists()) {
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        signingConfigs {
            katsunaConfig {
                storeFile rootProject.file("../../../vendor/keys/katsuna.keystore")
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        debug {
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
        }

        staging {
            initWith debug
            applicationIdSuffix '.staging'
            manifestPlaceholders = [providers_auth:"com.katsuna.staging"]
            matchingFallbacks = ['debug','release']
        }

        release {
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            signingConfig keystorePropertiesFile.exists() ? signingConfigs.katsunaConfig :
                    signingConfigs.debug
            lintOptions {
                disable 'MissingTranslation'
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':commons')
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'jp.wasabeef:picasso-transformations:2.1.2'
    implementation 'joda-time:joda-time:2.9.4'
    implementation "com.google.firebase:firebase-core:$firebaseVersion"
    implementation "com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion"

    testImplementation 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
